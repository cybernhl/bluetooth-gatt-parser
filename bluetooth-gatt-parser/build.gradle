import groovy.json.JsonBuilder

plugins {
    id "java-library"
    id "groovy-base"
    alias(libs.plugins.kotlin.jvm)
}

apply from: '../gradle/version.gradle'

group = 'org.sputnikdev'
version = default_version("").versionName
description = 'org.sputnikdev:bluetooth-gatt-parser'

sourceSets {
    main {
        java {
            srcDirs = ['../src/main/java']
        }
        resources {
            srcDirs = ['../src/main/resources']
        }
    }
    test {
        java {
            srcDirs = ['../src/test/java']
        }
        resources {
            srcDirs = ['../src/test/resources']
        }
    }
}

abstract class GenerateGattRegistryTask extends DefaultTask {

    @InputDirectory
    abstract DirectoryProperty getInputDirectory()

    @OutputFile
    abstract RegularFileProperty getOutputFile()

    @TaskAction
    void generate() {
        def output = getOutputFile().get().asFile
        // 確保目標資料夾存在
        output.parentFile.mkdirs()

        def parser = new XmlParser()
        parser.setFeature('http://apache.org/xml/features/disallow-doctype-decl', true)

        def registry = [:]// 使用 Groovy Map 來儲存結果
        // 遍歷輸入目錄中所有的 .xml 檔案
        getInputDirectory().get().asFile.eachFileMatch(~/.*\.xml/) { file ->
            def xml = parser.parse(file)
            def type = xml.attributes()['type']
            if ("${type}.xml" != file.name) {
                throw new IllegalStateException(
                        "GATT registry generation failed in task ${name}. 'type' attribute ($type) does not match its file name ($file.name)")
            }
            registry.put(xml.attributes()["uuid"], xml.attributes()["type"])
        }
        // 使用 JsonBuilder 將 Map 轉換為格式化的 JSON 字串並寫入檔案
        output.write(new JsonBuilder(registry).toPrettyString(), 'UTF-8')
    }
}

def gattResourcesDir = layout.projectDirectory.dir("../src/main/resources/gatt")
def gattBuildDir = layout.buildDirectory.get().dir("resources/main/gatt")

def generatorTasks = tasks.register("generateAllGattJson") {
    group = "Generator"
    description = "Generates all GATT JSON specification files."
}

def pathsToGenerate = ["characteristic", "service"] // 這兩個是從原始 pom.xml 的 build-helper-maven-plugin 推斷出的完整列表 & groovy 腳本中明確指定的
// 如果需要處理更多目錄，只需在此陣列中加入即可，例如：
// def pathsToGenerate = ["descriptor", "attribute",  "characteristic", "service"]
//["characteristic/descriptor", "characteristic/attribute", "descriptor", "attribute", "characteristic", "service"]
pathsToGenerate.each { path ->
    def taskName = "generateGattJsonFor${path.replace('/', '_').capitalize()}"
    def generatedTask = tasks.register(taskName, GenerateGattRegistryTask) {
        group = "Generator"
        description = "Generates GATT JSON for ${path}"

        inputDirectory.set(gattResourcesDir.dir(path))
        outputFile.set(gattBuildDir.file("${path}/gatt_spec_registry.json"))
    }
    // 將所有生成的 task 設為一個總 task 的依賴
    generatorTasks.configure { dependsOn(generatedTask) }
}

tasks.named("processResources") {
    dependsOn(generatorTasks)
}

////TODO this section modify from GattRegistryGenerator.groovy (/src/main/script)
//task GeneratorGattJson(group: "Generator", description: "GattRegistryGenerator Task") {
//    doLast {
//        generate("${this.projectDir.toString()}/src/main/resources/gatt/characteristic/descriptor",
//                "${this.project.buildDir.toString()}/resources/main/gatt/characteristic/descriptor/gatt_spec_registry.json")
//        generate("${this.projectDir.toString()}/src/main/resources/gatt/characteristic/attribute",
//                "${this.project.buildDir.toString()}/resources/main/gatt/characteristic/attribute/gatt_spec_registry.json")
//        generate("${this.projectDir.toString()}/src/main/resources/gatt/descriptor",
//                "${this.project.buildDir.toString()}/resources/main/gatt/descriptor/gatt_spec_registry.json")
//        generate("${this.projectDir.toString()}/src/main/resources/gatt/attribute",
//                "${this.project.buildDir.toString()}/resources/main/gatt/attribute/gatt_spec_registry.json")
//        generate("${this.projectDir.toString()}/src/main/resources/gatt/characteristic",
//                "${this.project.buildDir.toString()}/resources/main/gatt/characteristic/gatt_spec_registry.json")
//        generate("${this.projectDir.toString()}/src/main/resources/gatt/service",
//                "${this.project.buildDir.toString()}/resources/main/gatt/service/gatt_spec_registry.json")
//    }
//}
//
//static generate(String inputFolderName, String registryFileName) {
//    final File registryFile = new File(registryFileName)
//    registryFile.parentFile.mkdirs()
//    registryFile.createNewFile()
//    final File directory = new File(inputFolderName)
//    final XmlParser parser = new XmlParser()
//    parser.setFeature('http://apache.org/xml/features/disallow-doctype-decl', true)
//    final def registry = new HashMap()
//    directory.eachFileMatch(~/.*.xml/) { file ->
//        def xml = parser.parse(file)
//        def type = xml.attributes()['type']
//        if ("${type}.xml" != file.name) {
//            throw new IllegalStateException("GATT registry generation failed. 'type' attribute ($type) does not match to its file name ($file.name)")
//        }
//        registry.put(xml.attributes()["uuid"], xml.attributes()["type"])
//    }
//    registryFile.write(new groovy.json.JsonBuilder( registry).toPrettyString())
//}
//
//processResources.dependsOn(GeneratorGattJson)

java {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
}

kotlin {
    jvmToolchain(8)
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation libs.kotlin.stdlib
    api libs.org.slf4j.slf4j.api
    api libs.com.thoughtworks.xstream.xstream
    api libs.gson
    api libs.commons.beanutils.commons.beanutils
    api libs.com.google.guava.guava
    testImplementation libs.junit
    testImplementation libs.org.slf4j.slf4j.simple
    testImplementation libs.org.mockito.mockito.all
    testImplementation libs.org.powermock.powermock.module.junit4
    testImplementation libs.org.powermock.powermock.api.mockito
}

task showCurrentVersion {
    group "version Tasks"
    this.project.ext.currentVersion("")
}

task increaseVersion {
    group "version Tasks"
    doLast {
        this.project.ext.increaseVersion("")
    }
}

task decreaseVersion {
    group "version Tasks"
    doLast {
        this.project.ext.decreaseVersion("")
    }
}
